{-# OPTIONS --cubical #-}

open import Cubical.Core.Primitives hiding (_,_)

record CwF : Type₁ where
  field
    Ctxt : Type
    _⇒_ : Ctxt → Ctxt → Type
    Lvl : Ctxt → Type
    Ty : ∀ {Γ} → Lvl Γ → Type
    Tm : ∀ {Γ : Ctxt} (ℓ : Lvl Γ) → Ty ℓ → Type

    _<_ : ∀ {Γ} → Lvl Γ → Lvl Γ → Type
    <trans : ∀ {Γ} {j k ℓ : Lvl Γ} (p : j < k) (q : k < ℓ) → j < ℓ
    <prop : ∀ {Γ} {k ℓ : Lvl Γ} (p q : k < ℓ) → p ≡ q

    ∙ : Ctxt
    ε : ∀ {Γ} → Γ ⇒ ∙
    id : ∀ {Γ} → Γ ⇒ Γ
    _∘_ : ∀ {Γ Δ Ξ} → Δ ⇒ Ξ → Γ ⇒ Δ → Γ ⇒ Ξ

    εη : ∀ {Γ} (σ : Γ ⇒ ∙) → σ ≡ ε
    id∘ : ∀ {Γ Δ} (σ : Γ ⇒ Δ) → id ∘ σ ≡ σ
    ∘id : ∀ {Γ Δ} (σ : Γ ⇒ Δ) → σ ∘ id ≡ σ
    ∘∘ : ∀ {Γ Δ Ξ Θ} (σ : Ξ ⇒ Θ) (ρ : Δ ⇒ Ξ) (τ : Γ ⇒ Δ) → (σ ∘ ρ) ∘ τ ≡ σ ∘ (ρ ∘ τ)

    _⟦_⟧ : ∀ {Γ Δ} → Lvl Δ → Γ ⇒ Δ → Lvl Γ
    _⟦_⟧ᵀ : ∀ {Γ Δ} {ℓ : Lvl Δ} → Ty ℓ → (σ : Γ ⇒ Δ) → Ty (ℓ ⟦ σ ⟧)
    _⟦_⟧ᵗ : ∀ {Γ Δ} {ℓ : Lvl Δ} {A : Ty ℓ} → Tm ℓ A → (σ : Γ ⇒ Δ) → Tm (ℓ ⟦ σ ⟧) (A ⟦ σ ⟧ᵀ)

    ⟦id⟧ : ∀ {Γ} (ℓ : Lvl Γ) → ℓ ⟦ id ⟧ ≡ ℓ
    ⟦∘⟧ : ∀ {Γ Δ Ξ} (ρ : Δ ⇒ Ξ) (σ : Γ ⇒ Δ) (ℓ : Lvl Ξ) → ℓ ⟦ ρ ⟧ ⟦ σ ⟧ ≡ ℓ ⟦ ρ ∘ σ ⟧

    ⟦id⟧ᵀ : ∀ {Γ} {ℓ : Lvl Γ} (A : Ty ℓ) →
      (λ i → Ty (⟦id⟧ ℓ i)) [ A ⟦ id ⟧ᵀ ≡ A ]
    ⟦∘⟧ᵀ : ∀ {Γ Δ Ξ} (ρ : Δ ⇒ Ξ) (σ : Γ ⇒ Δ) {ℓ : Lvl Ξ} (A : Ty ℓ) →
      (λ i → Ty (⟦∘⟧ ρ σ ℓ i)) [ A ⟦ ρ ⟧ᵀ ⟦ σ ⟧ᵀ ≡ A ⟦ ρ ∘ σ ⟧ᵀ ]

    ⟦id⟧ᵗ : ∀ {Γ} {ℓ : Lvl Γ} {A : Ty ℓ} (a : Tm ℓ A) →
      (λ i → Tm (⟦id⟧ ℓ i) (⟦id⟧ᵀ A i)) [ a ⟦ id ⟧ᵗ ≡ a ]
    ⟦∘⟧ᵗ : ∀ {Γ Δ Ξ} (ρ : Δ ⇒ Ξ) (σ : Γ ⇒ Δ) {ℓ : Lvl Ξ} {A : Ty ℓ} (a : Tm ℓ A) →
      (λ i → Tm (⟦∘⟧ ρ σ ℓ i) (⟦∘⟧ᵀ ρ σ A i)) [ a ⟦ ρ ⟧ᵗ ⟦ σ ⟧ᵗ ≡ a ⟦ ρ ∘ σ ⟧ᵗ ]

    _∷_ : ∀ Γ {ℓ : Lvl Γ} → Ty ℓ → Ctxt
    _,_ : ∀ {Γ Δ} {ℓ : Lvl Δ} {A : Ty ℓ} (σ : Γ ⇒ Δ) → Tm (ℓ ⟦ σ ⟧) (A ⟦ σ ⟧ᵀ) → Γ ⇒ (Δ ∷ A)
    p : ∀ {Γ Δ} {ℓ : Lvl Δ} {A : Ty ℓ} → Γ ⇒ (Δ ∷ A) → Γ ⇒ Δ
    q : ∀ {Γ Δ} {ℓ : Lvl Δ} {A : Ty ℓ} (σ : Γ ⇒ (Δ ∷ A)) → Tm (ℓ ⟦ p σ ⟧) (A ⟦ p σ ⟧ᵀ)
    pβ : ∀ {Γ Δ} {ℓ : Lvl Δ} {A : Ty ℓ} (σ : Γ ⇒ Δ) (a : Tm (ℓ ⟦ σ ⟧) (A ⟦ σ ⟧ᵀ)) → p (σ , a) ≡ σ
    qβ : ∀ {Γ Δ} {ℓ : Lvl Δ} {A : Ty ℓ} (σ : Γ ⇒ Δ) (a : Tm (ℓ ⟦ σ ⟧) (A ⟦ σ ⟧ᵀ)) →
      (λ i → Tm (ℓ ⟦ pβ σ a i ⟧) (A ⟦ pβ σ a i ⟧ᵀ)) [ q (σ , a) ≡ a ]
    pqη : ∀ {Γ Δ} {ℓ : Lvl Δ} {A : Ty ℓ} (σ : Γ ⇒ (Δ ∷ A)) → (p σ , q σ) ≡ σ
    pq∘ : ∀ {Γ Δ Ξ} {ℓ : Lvl Ξ} {A : Ty ℓ} (σ : Δ ⇒ Ξ) (ρ : Γ ⇒ Δ) (a : Tm (ℓ ⟦ σ ⟧) (A ⟦ σ ⟧ᵀ)) →
      (σ , a) ∘ ρ ≡ ((σ ∘ ρ) , transp (λ i → Tm (⟦∘⟧ σ ρ ℓ i) (⟦∘⟧ᵀ σ ρ A i)) i0 (a ⟦ ρ ⟧ᵗ))
